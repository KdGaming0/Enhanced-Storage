plugins {
    // This plugin applies the correct loom variant based on the Minecraft version
    id("dev.kikugie.loom-back-compat")
    id("me.modmuss50.mod-publish-plugin")
}

// DO NOT set group = ...!
version = "${property("mod.version")}+${sc.current.version}"
base.archivesName = property("mod.id") as String

val requiredJava: JavaVersion = when {
    sc.current.parsed >= "26.1" -> JavaVersion.VERSION_25
    else -> JavaVersion.VERSION_25
}

// This can be used for publishing on Modrinth and Curseforge
val compatibleVersions: List<String> = sc.properties.rawOrNull("mod", "mc_releases")
    ?.asList().orEmpty().map { it.toString() }

repositories {
    /**
     * Restricts dependency search of the given [groups] to the [maven URL][url],
     * improving setup speed.
     */
    fun strictMaven(url: String, alias: String, vararg groups: String) = exclusiveContent {
        forRepository {
            maven(url) {
                name = alias
            }
        }
        filter {
            groups.forEach(::includeGroup)
        }
    }

    strictMaven(
        "https://api.modrinth.com/maven",
        "Modrinth",
        "maven.modrinth"
    )

    maven {
        url = uri("https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1")
    }

    exclusiveContent {
        forRepository {
            maven {
                url = uri("https://maven.azureaaron.net/releases")
            }
        }
        filter {
            includeGroup("net.azureaaron")
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Cassian's Maven"
                url = uri("https://maven.cassian.cc")
            }
        }
        filter {
            includeGroupAndSubgroups("cc.cassian")
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "shedaniel's Maven"
                url = uri("https://maven.shedaniel.me")
            }
        }
        filter {
            includeGroupAndSubgroups("me.shedaniel")
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "DAQEM Studios Maven"
                url = uri("https://maven.daqem.com/releases")
            }
        }
        filter {
            includeGroup("com.daqem.uilib")
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${sc.current.version}")
    modImplementation("net.fabricmc:fabric-loader:${property("deps.fabric_loader")}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${property("deps.fabric_api")}")
    modImplementation("com.daqem.uilib:uilib-fabric:${property("deps.uilib_version")}")

    modImplementation("maven.modrinth:midnightlib:${property("deps.midnightlib_version")}")
    include("maven.modrinth:midnightlib:${property("deps.midnightlib_version")}")

    modImplementation("net.azureaaron:hm-api:${property("deps.hm_api_version")}")
    include("net.azureaaron:hm-api:${property("deps.hm_api_version")}")

    implementation("org.msgpack:msgpack-core:0.9.12")

    // TODO add Soft integration
    // Soft integration with Roughly Enough Items: compile-only so REI stays optional at runtime
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api-fabric:26.1.819") {
        isTransitive = false
    }
    compileOnly("me.shedaniel.cloth:basic-math:0.6.1")

    // Skyblocker
    modCompileOnly("maven.modrinth:y6DuFGwJ:n5H2yDJu")
    // RRV
    modCompileOnly("maven.modrinth:5VolwT6c:8Xwd53bY")

    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:1.2.2")
    modRuntimeOnly("maven.modrinth:modmenu:${property("deps.modmenu_version")}")
}

loom {
    fabricModJsonPath = rootProject.file("src/main/resources/fabric.mod.json") // Useful for interface injection
    accessWidenerPath = rootProject.file("src/main/resources/enhanced_storage.accesswidener")

    decompilerOptions.named("vineflower") {
        options.put("mark-corresponding-synthetics", "1") // Adds names to lambdas - useful for mixins
    }

    runConfigs.all {
        vmArgs("-Dmixin.debug.export=true") // Exports transformed classes for debugging
        runDir = "../../run" // Shares the run directory between versions
    }
}

java {
    withSourcesJar()
    targetCompatibility = requiredJava
    sourceCompatibility = requiredJava

    toolchain {
        vendor = JvmVendorSpec.ADOPTIUM
        languageVersion = JavaLanguageVersion.of(requiredJava.majorVersion)
    }
}

tasks {
    processResources {
        fun MutableMap<String, String>.register(key: String, property: String) {
            val value: String = sc.properties[property]
            inputs.property(key, value)
            set(key, value)
        }

        val props = buildMap {
            register("id", "mod.id")
            register("name", "mod.name")
            register("version", "mod.version")
            register("minecraft", "mod.mc_compat")
            register("loader_version", "deps.fabric_loader")
            register("uilib", "deps.uilib_version")
        }

        filesMatching("fabric.mod.json") { expand(props) }

        val mixinJava = "JAVA_${requiredJava.majorVersion}"
        filesMatching("*.mixins.json") { expand("java" to mixinJava) }
    }

    // Builds the version into a shared folder in `build/libs/${mod version}/`
    register<Copy>("buildAndCollect") {
        group = "build"

        // loomx.mod(Sources)Jar returns the jar task for the applied loom variant
        from(loomx.modJar.map { it.archiveFile }, loomx.modSourcesJar.map { it.archiveFile })
        into(rootProject.layout.buildDirectory.file("libs/${project.property("mod.version")}"))
        dependsOn("build")
    }
}

if (sc.current.version in compatibleVersions) {
    val changelogFile = rootProject.file("CHANGELOG.md")
    val publishChangelog = if (changelogFile.exists()) changelogFile.readText() else "No changelog provided."

    publishMods {
        file.set(loomx.modJar.flatMap { it.archiveFile })
        additionalFiles.from(loomx.modSourcesJar.flatMap { it.archiveFile })

        displayName.set("${property("mod.name")} v${property("mod.version")} for mc${sc.current.version}")
        version.set("v${property("mod.version")}-mc${sc.current.version}")
        changelog.set(publishChangelog)
        type.set(BETA)
        modLoaders.add("fabric")

        dryRun.set(providers.environmentVariable("MODRINTH_TOKEN").getOrNull() == null)

        val modrinthId = providers.gradleProperty("publish.modrinth").orNull
        if (!modrinthId.isNullOrEmpty()) {
            modrinth {
                projectId.set(modrinthId)
                accessToken.set(providers.environmentVariable("MODRINTH_TOKEN"))
                minecraftVersions.addAll(compatibleVersions)
                requires { slug = "P7dR8mSH" } // Fabric API
                optional { slug = "mOgUt4GM" } // ModMenu
                embeds   { slug = "codAaoxh" } // MidnightLib
            }
        }

        /*
        val curseforgeId = providers.gradleProperty("publish.curseforge").orNull
        if (!curseforgeId.isNullOrEmpty()) {
            curseforge {
                projectId.set(curseforgeId)
                accessToken.set(providers.environmentVariable("CURSEFORGE_TOKEN"))
                minecraftVersions.addAll(compatibleVersions)
                client.set(true)
                server.set(true)

                requires("fabric-api")
            }
        }
        */
    }
}